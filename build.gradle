buildscript {
    ext {

        kotlin_version = '1.3.72'
        material_version = '1.3.0-alpha01'

        kotlinAndroid = [
                stdlib: "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        ]
        androidx = [
                appcompat       : "androidx.appcompat:appcompat:1.2.0-alpha02",
                activity        : "androidx.activity:activity:1.2.0-alpha06",
                fragment        : "androidx.fragment:fragment:1.3.0-alpha06",
                ktx             : "androidx.core:core-ktx:1.4.0-alpha01",
                recyclerview    : "androidx.recyclerview:recyclerview:1.2.0-alpha04",
                constraintlayout: "androidx.constraintlayout:constraintlayout:2.0.0-beta7",
                preference      : "androidx.preference:preference:1.1.1"
        ]

        koin = "org.koin:koin-androidx-scope:2.0.1"

        material = "com.google.android.material:material:$material_version"

        play = [
                core: "com.google.android.play:core:1.6.4",
                ktx : "com.google.android.play:core-ktx:1.6.4"
        ]

        licenses = "com.google.android.gms:play-services-oss-licenses:17.0.0"
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0-beta02'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.2'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

ext {
    def appMajor = 1
    def appMinor = 5
    def appPatch = 3
    def appBuild = 0

    def suffix = VersionSuffix.PRODUCT

    appVersionCode = appMajor * 1000000 + appMinor * 10000 + appPatch * 1000 + suffix.code * 100 + appBuild
    appVersionName = suffix == VersionSuffix.PRODUCT ?
            "${appMajor}.${appMinor}.${appPatch}" :
            "${appMajor}.${appMinor}.${appPatch}-${suffix.name} ${appBuild}"
}